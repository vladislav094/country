plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.0'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'com.google.protobuf' version '0.9.4'
}

group = 'guru.qa'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

dependencyManagement {
	imports {
		mavenBom 'org.springframework.grpc:spring-grpc-dependencies:0.4.0-SNAPSHOT'
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.data:spring-data-commons'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	runtimeOnly "org.postgresql:postgresql:42.7.3"
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	// migrations
	runtimeOnly "org.flywaydb:flyway-core:10.17.1"
	runtimeOnly "org.flywaydb:flyway-database-postgresql:10.17.1"
	// graphQl
	implementation 'org.springframework.boot:spring-boot-starter-graphql'
	implementation "com.tailrocks.graphql:graphql-datetime-spring-boot-starter:6.0.0"
	// grpc
	implementation 'org.springframework.grpc:spring-grpc-spring-boot-starter'
	implementation "io.grpc:grpc-services"
	implementation "com.google.protobuf:protobuf-java-util:${dependencyManagement.importedProperties['protobuf-java.version']}"
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${dependencyManagement.importedProperties['protobuf-java.version']}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${dependencyManagement.importedProperties['grpc.version']}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {
				option 'jakarta_omit'
				option '@generated=omit'
			}
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
